<program>::=<commands> <gotothemoon>

<commands>::=<command> <commands>|

<command>::=<def>|<int_assign>|<string_assign>|<arith_exp>|<condition>|<loop>|<comment>|<print>

<condition>::="rly" <logic_exp> <open_block> <commands> <close_block>|"rly" <logic_exp> <open_block> <commands> <close_block> "but" <els>

<els>::=<condition>|<open_block> <commands> <close_block>|

<open_block>::='{'

<close_block>::='}'

<loop>::="many" <logic_exp> <open_block> <commands> <close_block>

<gotothemoon>::="plz" <arith_exp> "go to the moon"

<print>::="wow" <arith_exp> "wow"|"wow" string "wow"

<def>::="very" id "so words"|"very" id "so numbr"

<int_assign>::= id "is" <arith_exp>|id "is want numbr"

<string_assign>::=id "is" string

<arith_exp>::=<ea>

<ea>::=<ea> "more" <ta>|<ea> "less" <ta>|<ta>

<ta>::=<ta> LOTS <fa>|<ta> FEW <fa>|<fa>

<fa>::=	'(' <ea> ')'|	number | id

<logic_exp>::=<el>

<el>::=<el> "or" <tl> |	<tl>

<tl>::=<tl> "and" <fl>	|	<fl>

<fl>::="not" <relational_exp>	|	<relational_exp>	|	'(' <el> ')'

<relational_exp>::=<arith_exp> "bigger" <arith_exp>	| <arith_exp> "smaller" <arith_exp>	| <arith_exp> "biggerish" <arith_exp>	| <arith_exp> "smallerish" <arith_exp>	| <arith_exp> "same" <arith_exp>
